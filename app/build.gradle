import java.nio.file.Paths

plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.firebase.firebase-perf'
    id 'androidx.navigation.safeargs'
}


def userHome = Paths.get(System.getProperty('user.home'))
def keystorePath = userHome.resolve('.stax/keystore.properties')
def keystorePropertiesFile = file(keystorePath)

def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        releaseConfig {
            file(keystorePath).with { propertiesFile ->
                if (propertiesFile.canRead()) {
                    keyAlias keystoreProperties['keyAlias']
                    keyPassword keystoreProperties['keyPassword']
                    storeFile file(keystoreProperties['storeFile'])
                    storePassword keystoreProperties['storePassword']
                }
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileSdk 31

    defaultConfig {
        applicationId "com.hover.stax"
        minSdk 21
        targetSdk 31
        versionCode 133
        versionName "1.10.1"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }

    buildTypes {
        debug {
            FirebasePerformance {
                instrumentationEnabled false
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            file(keystorePath).with { propertiesFile ->
                if (propertiesFile.canRead()) {
                    signingConfig signingConfigs.releaseConfig
                }
            }
        }
    }

    bundle {
        language {
            //Ensures all language string resources is bundled in the aab.
            enableSplit = false
        }
    }


    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    lint {
        disable 'MissingTranslation', 'ExtraTranslation'
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Google
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.biometric:biometric:1.1.0"
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.activity:activity-compose:1.4.0'
    kapt "androidx.lifecycle:lifecycle-common-java8:2.4.1"

    implementation 'androidx.core:core-splashscreen:1.0.0-beta02'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    //logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:28.4.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-inappmessaging-display'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.android.play:core:1.10.3'
    implementation 'com.google.firebase:firebase-perf'
    implementation 'com.google.firebase:firebase-firestore-ktx'

    //auth
    implementation 'com.google.android.gms:play-services-auth:20.2.0'
    implementation 'com.google.firebase:firebase-auth-ktx'

    implementation 'com.amplitude:android-sdk:2.23.2'
    implementation "com.squareup.okhttp3:okhttp:4.8.1"
    implementation "com.googlecode.libphonenumber:libphonenumber:8.12.9"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation "com.github.YarikSOffice:lingver:1.3.0"
    implementation 'com.appsflyer:af-android-sdk:6.2.3'
    implementation 'com.android.installreferrer:installreferrer:2.2'

    implementation 'com.github.bumptech.glide:glide:4.13.0'
    kapt 'com.github.bumptech.glide:compiler:4.13.0'

    def roomVersion = "2.4.2"
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    implementation "androidx.core:core-ktx:1.7.0"

    //di
    def koin_version= "3.1.5"
    implementation "io.insert-koin:koin-android:$koin_version"

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Hover SDK
    debugImplementation project(":hover.sdk")
    def sdk_version = "2.0.0-stax-1.10.1-pro"

    releaseImplementation "com.hover:android-sdk:$sdk_version"

    debugImplementation 'com.android.volley:volley:1.2.1'
    debugImplementation 'com.google.android.gms:play-services-analytics:18.0.1'

    debugImplementation 'io.sentry:sentry-android:4.3.0'

    //compose
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}