import java.nio.file.Paths

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'


def userHome = Paths.get(System.getProperty('user.home'))
def keystorePath = userHome.resolve('.stax/keystore.properties')
def keystorePropertiesFile = file(keystorePath)

def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        releaseConfig {
            file(keystorePath).with { propertiesFile ->
                if (propertiesFile.canRead()) {
                    keyAlias keystoreProperties['keyAlias']
                    keyPassword keystoreProperties['keyPassword']
                    storeFile file(keystoreProperties['storeFile'])
                    storePassword keystoreProperties['storePassword']
                }
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.hover.stax"
        minSdkVersion 18
        targetSdkVersion 30
        versionCode 108
        versionName "1.8.1.3"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            buildConfigField "String", "SELF_DESTRUCT", "\"1622450539\""
        }

        release {
            minifyEnabled true
            shrinkResources true
            buildConfigField "String", "SELF_DESTRUCT", "\"1625097599\"" // June 30, 2021
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            file(keystorePath).with { propertiesFile ->
                if (propertiesFile.canRead()) {
                    signingConfig signingConfigs.releaseConfig
                }
            }
        }
    }

    bundle {
        language {
            //Ensures all language string resources is bundled in the aab.
            enableSplit = false
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        abortOnError false
    }

    buildFeatures {
        viewBinding true
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Google
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.biometric:biometric:1.1.0"
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.6.0'
    kapt "androidx.lifecycle:lifecycle-common-java8:2.4.0"

    implementation "androidx.room:room-runtime:2.3.0"
    kapt "androidx.room:room-compiler:2.3.0"
    androidTestImplementation "androidx.room:room-testing:2.3.0"

    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:28.4.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-inappmessaging-display'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.android.play:core:1.10.2'
    implementation 'com.google.firebase:firebase-perf'
    implementation 'com.google.firebase:firebase-firestore-ktx'

    //auth
    implementation 'com.google.android.gms:play-services-auth:19.2.0'
    implementation 'com.google.firebase:firebase-auth-ktx'

    implementation 'com.amplitude:android-sdk:2.23.2'
    implementation "com.squareup.okhttp3:okhttp:4.8.1"
    implementation "com.googlecode.libphonenumber:libphonenumber:8.12.9"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.chauthai.swipereveallayout:swipe-reveal-layout:1.4.1'
    implementation "com.github.YarikSOffice:lingver:1.3.0"
    implementation 'com.appsflyer:af-android-sdk:6.2.3'
    implementation 'com.android.installreferrer:installreferrer:2.2'

    // Kotlin
    implementation "androidx.room:room-ktx:2.3.0"
    implementation "androidx.core:core-ktx:1.6.0"

    //di
    def koin_version= "2.2.2"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "org.koin:koin-androidx-fragment:$koin_version"

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Hover SDK
    debugImplementation project(":hover.sdk")
    def sdk_version = "2.0.0-stax-1.5.7.0-pro"

    releaseImplementation "com.hover:android-sdk:$sdk_version"
    debugImplementation 'com.android.volley:volley:1.2.1'
    debugImplementation 'com.google.android.gms:play-services-analytics:17.0.1'

    debugImplementation 'io.sentry:sentry-android:4.3.0'
}
